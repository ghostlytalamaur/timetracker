apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file(".key/keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        signApk {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(".key/" + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        applicationId "mvasoft.timetracker.new"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 4
        versionName "2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Specify string resource: @string/app_name which used in AndroidManifest.xml
        resValue "string", "app_name", "TimeTracker 2"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString(),
                             eventBusIndex : 'mvasoft.timetracker.TimeTrackerEventBusIndex'
                ]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "TimeTracker Debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signApk
        }
    }
    compileOptions {
        sourceCompatibility build_versions.source_compatibility
        targetCompatibility build_versions.target_compatibility
    }
    dexOptions {
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'android/databinding/DataBinderMapper.java'
        exclude 'android/databinding/DataBindingComponent.java'
        exclude 'android/databinding/DynamicUtil.java'
    }
}

dependencies {
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    annotationProcessor deps.dagger.compiler
    annotationProcessor deps.dagger.android_support_compiler

    implementation deps.rxjava.rxjava2
    implementation deps.androidx.lifecycle.extensions
    implementation deps.androidx.lifecycle.reactivestreams
    implementation deps.androidx.lifecycle.java8

    implementation deps.androidx.room.runtime
    implementation deps.androidx.room.rxjava
    annotationProcessor deps.androidx.room.compiler

    implementation deps.androidx.appcompat
    implementation deps.androidx.constraintlayout.constraintlayout

    implementation deps.androidx.preference.preference

    implementation deps.androidx.legacy.support
    implementation deps.androidx.legacy.preference

    implementation deps.androidx.recyclerview.recyclerview
    implementation deps.androidx.recyclerview.selection
    implementation deps.material


    implementation deps.androidx.navigation.fragment
    implementation deps.androidx.navigation.ui

    implementation deps.gson
    implementation deps.androidx.work.work

    implementation deps.eventbus.eventbus
    annotationProcessor deps.eventbus.compiler

    implementation deps.materialcalendarview

    implementation 'com.jakewharton.timber:timber:4.7.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    testImplementation deps.junit.junit
    androidTestImplementation deps.androidx.espresso.core
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.joda_time

    implementation project(path: ':datetimepicker')
    implementation project(path: ':utils')
    implementation project(path: ':recyclerbinding')
}
